import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlin.parcelize)
}

apply from: rootProject.file('gradle/versioning.gradle')

version = getVersionFromFile()

android {
    namespace = 'com.auth0.android.ui_components'
    compileSdk = 36

    defaultConfig {
        minSdk = 30
        versionName project.version

        buildConfigField "String", "LIBRARY_NAME", "\"$project.rootProject.name\""
        buildConfigField "String", "VERSION_NAME", "\"${project.version}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        manifestPlaceholders += [auth0Domain: "YOUR_DOMAIN",
                                 auth0Scheme: "YOUR_SCHEME"]
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        htmlReport = true
        abortOnError = true
    }
    testOptions {
        unitTests {
            all {
                maxHeapSize = "1024m"
            }
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }
}

dependencies {

    implementation libs.auth0
    implementation libs.androidx.lifecycle.runtime.ktx

    //compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.compose.navigation
    implementation libs.viewmodel
    implementation libs.kotlinx.serialization.json
    implementation libs.androidx.compose.material.icon

    // Credential Manager for Passkey support
    implementation libs.androidx.credentials
    implementation libs.androidx.credentials.play.services.auth

    //qr-code
    implementation libs.zxing.barcode


    // JUnit 4
    testImplementation libs.junit4
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.truth
}

apply from: rootProject.file('gradle/maven-publish.gradle')